# generate xml from an html form

one of the cool things about xforms is that i can abstract the data model from the form and get a consistent view of my xml. for me this is the killer feature about xforms. however, regular html forms are way more pervasive and i found myself thinking on how i could implement this feature in standard html.

in xforms we have a model (which is xml) and also a form that acts on that model. so our form "knows" the xml structure. in html forms there's no notion of data model implicit, or anything like that. what is submitted from an html form is a set of key value pairs.


in this little article we are going to design an application that can insert and search multiple choice questions using html. the html form will be responsible for the insert. the search will be tackled with application builder in part two of this article.

## part 1: creating the form

for the sake of this demonstration let's assume 'option_a' is always the correct option, thus avoiding another control. this is ok as we can randomize this list in the server side once we receive the options.

so while in xforms we would submit something like:

``` xml
<question>
  <text>which of the following twitter users works for marklogic?</text>
  <answer>
    <a>peteaven</a>
    <b>jchris</b>
    <c>stuhood</c>
    <d>antirez</d>
  </answer>
</question>
```

in regular html you have something like:

```
> POST / HTTP/1.1
> Content-Type: application/x-www-form-urlencoded
   question=Which of the following twitter users works for MarkLogic?
   &option_a=peteaven&option_b=jchris&option_c=stuhood&option_d=antirez
```

while this can map perfectly to a relational database it doesn't play well with xml. let me rephrase this: there are multiple ways you could shape it as xml.

one possible solution is to name the fields with an xpath expression and then generate an xml tree out of this path expression.

once we solve this we have two options on how to generate the xml from xpath: do some work with a client-side language like javascript and produce the xml that is sent to the server or simply submit the form and create the xml on the server-side with xquery. i choose the second approach for two reasons:

  1. to push the xquery high order functions support in marklogic server to the limit and learn how far it can go.
  2. other people might have a similar problem that needs to be solved in the server side. this way they can reuse the code.

high order functions are functions that take functions as parameters. 

two examples of such functions are fold (a.k.a. reduce or inject) and map (a.k.a. collect or transform). 

fold is a list destructor. you give it a list l, a starting value z and a function f. then the fold starts accumulating the value of applying f to each element of l in z. map is a function that applies a function f to each element of a list.

an example of a fold might be implementing sum, a function that sums the contents of a list:

``` haskell
# in no particular language, pseudo code
sum l = fold (+) 0 l
```

an example of a map is multiply every element in a list by two:

```
# in no particular language, pseudo code
double l = map (2*) l
```

a fold is really just a list destructor. but you can generalize it for any  arbitrary algebraic data types. 
you call these "generic folds" a catamorphism. actually a fold is just a catamorphism on lists.

implementing these functions in marklogic xquery 1.0 with recursion is really easy:

``` xml
declare function local:head( $l ) { $l[1] } ;
declare function local:tail( $l ) { fn:subsequence( $l, 2 ) } ;
declare function local:fold( $f, $z, $l ) { 
  if( fn:empty( $l ) ) then $z
  else local:fold( $f,
                   xdmp:apply( $f, $z, local:head( $l ) ),
                   local:tail( $l ) ) } ;

declare function local:map( $f, $l ) {
  for $e in $l return xdmp:apply( $f, $e ) } ;

declare function local:add($x, $y)         { $x + $y } ;
declare function local:multiply($x, $y)    { $x * $y } ;
declare function local:multiply-by-two($x) { $x * 2 } ;

(: sums a list using fold :)
declare function local:sum( $l ) {
  let $add      := xdmp:function( xs:QName( 'local:add' ) )
  return local:fold( $add, 0, $l ) } ;

declare function local:double ( $l ) {
  let $multiply-by-two := 
    xdmp:function( xs:QName( 'local:multiply-by-two' ) )
  return local:map( $multiply-by-two, $l ) } ;

(: factorial just for fun :)
declare function local:fact($n) { 
  let $multiply := xdmp:function(xs:QName('local:multiply'))
  return local:fold($multiply, 1, 1 to $n) };

(: This is the main part of the XQuery file
 : Illustrating the fold and map from the previous listing :)
<tests>
  <!-- fun facts: http://www.mathematische-basteleien.de/triangularnumber.htm -->
  <sum> { local:sum(1 to 100) } </sum>
  <fact> { local:fact( 10 ) } </fact>
  <double> { local:double( (1 to 5) ) } </double>
</tests>
```

so how can we use all of this to solve our xpath to xml problem? simple. we need to destruct the list of xpaths and generate a tree. in other words, we need to fold the list intro a tree.

if we go one level down an xpath is really a list of steps. once again we need to destruct that list to create each node. so we need a fold inside a fold.

we now need to iterate the list of field values, navigate to the corresponding node using the xpath expression, and finally replace the value of the node (empty at this point) with the value provided in the http form.

scared? wondering if we really need all this functional stuff? fear not, problem is solved and we will simply use a xquery library module that already exists to solve the problem! hooray. 

the library is called generate-tree and is included in the dxc github project. to get it simply install git and:

```
git clone git://github.com/dscape/dxc.git
```

if you don't know what git is (neither you care) simply go to the project page at http://github.com/dscape/dxc and download the source.

if you are curious to see the implementation using the folds and everything you learned so far you can check the the [gen-tree.xqy](http://github.com/dscape/dxc/blob/master/func/gen-tree.xqy) implementation at github. or as an exercise you can try and do it yourself! to run this code directly from cq i created [another script](http://gist.github.com/364356) that creates a tree while printing out debug messages. this might be useful to understand how the code is running without getting "lost in recursion".

create a folder called 'questions-form' and place the dxc code there:

```
njob@ubuntu:~/Desktop/questions-form$ ls -l
total 8
drwxr-xr-x 12 njob njob 4096 2010-08-13 20:51 dxc
-rw-r--r--  1 njob njob  149 2010-08-13 20:59 index.xqy
```

now we need to create the html form. for now simply create a file called index.xqy inside the 'questions-form' directory and insert the following code:

``` xml
xquery version '1.0-ml';

"Hello World!"
```

in this listing we simply print hello world! to get our website online simply go the the marklogic server administration interface at http://localhost:8001 and create a new application server with the following parameters:

```
name: questions-form
port: <any port that is available in your system>
root: <full path of the directory where you have the index.xqy file>
```

in my case this will be:

```
port: 6173
root: /home/njob/Desktop/questions-form
```

if you have cq installed you can simplify the process by running the following script (remember to change the root. also change the port if necessary)

``` xml
xquery version '1.0-ml';

import module namespace admin = "http://marklogic.com/xdmp/admin" 
  at "/MarkLogic/admin.xqy" ;

let $name       := "questions-form"
let $root       := "/home/njob/Desktop/questions-form"
let $port       := 6173
let $config     := admin:get-configuration()
let $db         := "Documents"
let $groupid    := admin:group-get-id( $config, "Default" )
let $new        := admin:http-server-create( $config, $groupid, $name, 
  $root, xs:unsignedLong( $port ), 0, xdmp:database( $db ) )
return ( admin:save-configuration( $new ) ,
         <div class="message">
           An HTTP Server called {$name} with root {$root} on 
           port {$port} created successfully </div> )
```

this is running against the default documents database. this is ok for a demonstration but in a realistic scenario you would be using your own database.

now when you visit http://localhost:6173 you will get a warm hello world! 

now let's change the code to actually perform the transformation. to do so simply insert this code in index.xqy. feel free to inspect it and learn from it - i commented it just for that reason.

``` xml
xquery version '1.0-ml';

(: First we import the library that generates the tree :)
import module namespace mvc = "http://ns.dscape.org/2010/dxc/mvc"
  at "dxc/mvc/mvc.xqy" ;

(: 
 : This function receives a string as the parameter $o
 : which will be either 'a', 'b', 'c' or 'd' and
 : generates an input field for the form
 :)
declare function local:generate-option( $o ) {
 (<br/>, <label for="/question/answer/{$o}">{$o}) </label>,
      <input type="text" name="/question/answer/{$o}" 
        id="/question/answer/{$o}" size="50"/>) };

(: This function simply displays an html form as described in the figures :)
declare function local:display-form() {
  <form name="question_new" method="POST" action="/" id="question_new">
    <label for="/question/text">Question</label><br/>
    &nbsp;&nbsp;&nbsp; <textarea name="/question/text" id="/question/text" 
      rows="2" cols="50">
    Question goes here </textarea>
  <br/>
  { (: using the generate option function button to generate four fields :)
    for $o in ('a','b','c','d') return local:generate-option( $o ) }
  <br/><br/><input type="submit" name="submit" id="submit" value="Submit"/>
   </form> } ;

(: this function will process the insert and display the result
 : for now it simply shows the tree that was generated from the HTML form
 :)
declare function local:display-insert() {
  xdmp:quote( mvc:tree-from-request-fields() ) } ;

(: Now we set the content type to text html so the browser renders
 : the page as HTML as opposed to XML :)
xdmp:set-response-content-type("text/html"),
<html>
  <head>
    <title>New Question</title>
  </head>
  <body> {
  (: if it's a post then the user submited the form :)   
  if( xdmp:get-request-method() = "POST" )
  then local:display-insert()
  else
    (: the user wants to create a new question :)
    local:display-form() }
  </body>
</html>
```

we are using the 'mvc:tree-from-request-fields()' function to create the tree from the request fields. however this function isn't described in [gen-tree.xqy](http://github.com/dscape/dxc/blob/master/func/gen-tree.xqy). this is declare in another library called mvc: 

``` xml
declare function mvc:tree-from-request-fields() {
  let $keys   := xdmp:get-request-field-names() [fn:starts-with(., "/")]
  let $values := for $k in $keys return xdmp:get-request-field($k)
  return gen:process-fields( $keys, $values ) } ;
```

now you can visit http://localhost:6173 again and you'll see our form. fill it accordingly to the following picture and click "submit"

this is how the document you inserted looks like:

``` xml
<?xml version="1.0" encoding="utf-8"?>
<question>
  <text>which of the following twitter users works for marklogic?</text>
  <answer>
    <a>peteaven</a>
    <b>jchris</b>
    <c>stuhood</c>
    <d>antirez</d>
  </answer>
</question>
```

now let's augment our form with some more interesting fields like author and difficulty. this will help make our search application interesting.
simply update the display-form function:

``` xml
(: This function simply displays an html form as described in the figures :)
declare function local:display-form() {
  <form name="question_new" method="POST" action="/" id="question_new">
    <input type="hidden" name="/question/created-at" 
      id="/question/created-at" value="{fn:current-dateTime()}"/>
    <input type="hidden" name="/question/author" 
      id="/question/author" value="{xdmp:get-current-user()}"/>
    <br/> <label for="/question/difficulty">Difficulty: </label>
      <input type="text" name="/question/difficulty" 
        id="/question/difficulty" size="50"/>
    <br/> <label for="/question/topic">Topic:&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;
    </label>
      <input type="text" name="/question/topic" 
        id="/question/topic" size="50"/>
    <br/><br/> <label for="/question/text">Question</label><br/>
    &nbsp;&nbsp;&nbsp; <textarea name="/question/text" id="/question/text" 
      rows="2" cols="50">
    Question goes here </textarea>
  <br/>
  { (: using the generate option function button to generate four fields :)
    for $o in ('a','b','c','d') return local:generate-option( $o ) }
  <br/><br/><input type="submit" name="submit" id="submit" value="Submit"/>
   </form> } ;
```

now we are missing the part where we actually insert the document in the database. for that we need to update the function that local:display-insert() function:

``` xml
(: this function will process the insert and display the result
 : it then redirects to / giving you the main page
 :)
declare function local:display-insert() {
  try {
    let $question   := mvc:tree-from-request-fields() (: get tree :)
      let $author     := if ($question//author[1]) 
                         then fn:concat($question//author[1], "/") else ()
      (: now we insert the document :)
      let $_          := xdmp:document-insert(
        (: this fn:concat is generating a uri with directories
         : e.g. /questions/njob/2362427670145529782.xml 
         :)
        fn:concat("/questions/", $author, xdmp:random(), ".xml") , $question )
      return  xdmp:redirect-response("/?flash=Insert+OK")
  } catch ($e) {
    xdmp:redirect-response(fn:concat("/?flash=", 
      fn:encode-for-uri($e//message/text()))) } } ;
```

so far we talked about the problem, differences with xforms, proceeded to talk on high order functions and how to implement it in xquery and finally we got a working solution for our little problem. coming up next we are going to build an application to search these questions we can now insert with application builder. then we are going to take advantage of the new functionalities available in marklogic 4.2. to extend application builder with this form.